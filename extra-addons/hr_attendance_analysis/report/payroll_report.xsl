<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:fo="http://www.w3.org/1999/XSL/Format">

    <xsl:template match="/">
        <xsl:call-template name="rml" />
    </xsl:template>

    <xsl:template name="rml">
		<document filename="example.pdf">
			<template pageSize="29.7cm,21cm" leftMargin="1.0cm" rightMargin="1.0cm" topMargin="1.0cm" bottomMargin="1.0cm"
				title="Payrolls" author="Generated by Moldeo Int, Cristian S. Rocha" allowSplitting="20">
				<pageTemplate id="first">
					<pageGraphics>
						<setFont name="Helvetica-Bold" size="16"/>
						<drawCentredString x="15.0cm" y="20.0cm" t="1">Reporte de liquidaci√≥n</drawCentredString>
						<setFont name="Helvetica" size="8"/>
						<drawRightString x="29.0cm" y="20.0cm"><xsl:value-of select="/report/date"/></drawRightString>
					</pageGraphics>
					<frame id="col1" x1="1.0cm" y1="1.0cm" width="27.7cm" height="19.0cm"/>
				</pageTemplate>
			</template>

			<stylesheet>
				<paraStyle name="small" fontName="Helvetica" fontSize="6"/>
				<paraStyle name="normal" fontName="Helvetica" fontSize="12"/>
				<paraStyle name="big" fontName="Helvetica" fontSize="24"/>
				<blockTableStyle id="name">
					<blockFont name="Helvetica-BoldOblique" size="12" leading="12"/>
					<blockAlignment value="LEFT" start="0,0" stop="-2,0"/>
					<blockAlignment value="RIGHT" start="-1,0" stop="-1,0"/>
                    <blockBottomPadding length="10" start="0,0" stop="-1,-1"/>
				</blockTableStyle>
				<blockTableStyle id="payroll">
					<blockFont name="Helvetica-BoldOblique" size="8" start="0,0" stop="-1,0"/>
					<blockFont name="Helvetica" size="5" start="0,1" stop="-1,-1"/>
					<blockBackground colorName="grey" start="0,0" stop="-1,0"/>
					<lineStyle kind="LINEABOVE" colorName="grey" start="0,0" stop="-1,0" />
					<lineStyle kind="LINEBEFORE" colorName="grey" start="0,0" stop="-1,-1"/>
					<lineStyle kind="LINEAFTER" colorName="grey" start="-1,0" stop="-1,-1"/>
					<lineStyle kind="LINEBELOW" colorName="grey" start="0,-1" stop="-1,-1"/>
					<blockValign value="TOP"/>
				</blockTableStyle>
			</stylesheet>

			<story>
                <xsl:apply-templates select="report/payroll"/>
			</story>
		</document>
    </xsl:template>

    <xsl:template match="payroll">
	<blockTable colWidths="25,250,250" style="name">
        <tr>
            <td>
                <xsl:if test="state = 'draft'">&#9997;</xsl:if>
                <xsl:if test="state = 'valid'">&#10003;</xsl:if>
                <xsl:if test="state = 'cancel'">&#10007;</xsl:if>
            </td>
            <td> <xsl:value-of select="name"/> </td>
            <td>
                <xsl:value-of select="date_from"/>-<xsl:value-of select="date_to"/>
            </td>
        </tr>
    </blockTable>
	<blockTable style="payroll">
        <xsl:attribute name="colWidths"><xsl:value-of select="columns/widths"/></xsl:attribute>
            <tr>
                <xsl:for-each select="columns/column">
                    <td><para style="small" t="1"><xsl:value-of select="."/></para></td>
                </xsl:for-each>
            </tr>
            <xsl:for-each select="lines/line">
                <xsl:variable name="line" select="@id"/>
                <tr>
                <xsl:for-each select="../../columns/column">
                    <xsl:variable name="field" select="@id"/>
                    <td><para style="small"><xsl:value-of select="../../lines/line[@id=$line]/field[@id=$field]"/></para></td>
                </xsl:for-each>
            </tr>
        </xsl:for-each>
    </blockTable>
    <para style="small"><b t="1">Note: </b> <xsl:value-of select="note"/></para>
    </xsl:template>
</xsl:stylesheet>

<!--
     vim:expandtab:smartindent:tabstop=4:softtabstop=4:shiftwidth=4:
     -->
